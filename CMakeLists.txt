CMAKE_MINIMUM_REQUIRED(VERSION 3.0)
PROJECT(J2534)

SET(J2534_MAJOR_VERSION 0)
SET(J2534_MINOR_VERSION 1)
SET(J2534_PATCH_VERSION 0)
SET(J2534_VERSION ${J2534_MAJOR_VERSION}.${J2534_MINOR_VERSION}.${J2534_PATCH_VERSION})

SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -std=c++11")
SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -pedantic -Wextra -Werror")

# J2534
SET(J2534_SOURCE_FILES ${J2534_SOURCE_FILES} j2534_v0404.h)
SET(J2534_SOURCE_FILES ${J2534_SOURCE_FILES} j2534.h j2534.cpp)

SET(LIB_TYPE SHARED)
ADD_LIBRARY(j2534 ${LIB_TYPE} ${J2534_SOURCE_FILES})
SET_TARGET_PROPERTIES(j2534 PROPERTIES COMPILE_FLAGS "-DJ2534_EXPORTS")

# SWIG
FIND_PACKAGE(SWIG REQUIRED)
FIND_PACKAGE(PythonInterp REQUIRED)
FIND_PACKAGE(PythonLibs ${PYTHON_VERSION_STRING} REQUIRED)
SET(CMAKE_SWIG_FLAGS -I${PYTHON_INCLUDE_PATH})

# SWIG J2534
INCLUDE(${SWIG_USE_FILE})
SET_SOURCE_FILES_PROPERTIES(j2534.i PROPERTIES CPLUSPLUS ON)
SWIG_ADD_MODULE(j2534_python python j2534.i)
SWIG_LINK_LIBRARIES(j2534_python j2534)
SWIG_LINK_LIBRARIES(j2534_python ${PYTHON_LIBRARIES})
TARGET_INCLUDE_DIRECTORIES(${SWIG_MODULE_j2534_python_REAL_NAME} PUBLIC ${CMAKE_CURRENT_SOURCE_DIR})
TARGET_INCLUDE_DIRECTORIES(${SWIG_MODULE_j2534_python_REAL_NAME} PUBLIC ${PYTHON_INCLUDE_PATH})
SET_TARGET_PROPERTIES(${SWIG_MODULE_j2534_python_REAL_NAME} PROPERTIES OUTPUT_NAME "j2534")
SET_TARGET_PROPERTIES(${SWIG_MODULE_j2534_python_REAL_NAME} PROPERTIES PREFIX "_")
SET_TARGET_PROPERTIES(${SWIG_MODULE_j2534_python_REAL_NAME} PROPERTIES COMPILE_FLAGS "-Wno-error=missing-field-initializers -D_hypot=hypot")

#
# Install
#

INCLUDE(CMakePackageConfigHelpers)
WRITE_BASIC_PACKAGE_VERSION_FILE("${CMAKE_CURRENT_BINARY_DIR}/J2534/J2534ConfigVersion.cmake" VERSION ${J2534_VERSION} COMPATIBILITY AnyNewerVersion)

# Install swig stuff
SET(SwigPackageLocation lib/swig/J2534)
INSTALL(FILES j2534.i DESTINATION ${SwigPackageLocation})

# Install python stuffs
find_package(PythonInterp ${PYTHON_VERSION} REQUIRED)
find_package(PythonLibs ${PYTHON_VERSION} REQUIRED)
execute_process(COMMAND ${PYTHON_EXECUTABLE} -c "from distutils.sysconfig import get_python_lib; import sys; print('/'+get_python_lib(standard_lib=True, prefix='')+'/site-packages')" OUTPUT_VARIABLE PYTHON_SITE_PACKAGES OUTPUT_STRIP_TRAILING_WHITESPACE)
INSTALL(TARGETS ${SWIG_MODULE_j2534_python_REAL_NAME} DESTINATION ${CMAKE_INSTALL_PREFIX}${PYTHON_SITE_PACKAGES})
INSTALL(FILES ${CMAKE_CURRENT_BINARY_DIR}/j2534.py DESTINATION ${CMAKE_INSTALL_PREFIX}${PYTHON_SITE_PACKAGES})

# Install library stuffs
INSTALL(TARGETS j2534 EXPORT j2534_targets LIBRARY DESTINATION lib ARCHIVE DESTINATION lib RUNTIME DESTINATION bin  INCLUDES DESTINATION include)
INSTALL(FILES j2534.h j2534_v0404.h DESTINATION include)

# Install cmake stuffs
EXPORT(EXPORT j2534_targets FILE "${CMAKE_CURRENT_BINARY_DIR}/J2534Targets.cmake")
SET(ConfigPackageLocation lib/cmake/J2534)
INSTALL(EXPORT j2534_targets FILE J2534Targets.cmake DESTINATION ${ConfigPackageLocation})
INSTALL(FILES cmake/J2534Config.cmake "${CMAKE_CURRENT_BINARY_DIR}/J2534/J2534ConfigVersion.cmake" DESTINATION ${ConfigPackageLocation})
